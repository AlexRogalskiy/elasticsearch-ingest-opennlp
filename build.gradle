import java.nio.file.Files
import de.undercouch.gradle.tasks.download.Download

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
 }

  dependencies {
    classpath "org.elasticsearch.gradle:build-tools:${elasticsearchVersion}"
  }
}

plugins {
  id "de.undercouch.download" version "4.1.1"
  id "co.riiid.gradle" version "0.4.2"
  id "com.github.ben-manes.versions" version '0.33.0'
}

group = 'de.spinscale.elasticsearch.plugin.ingest'
version = "${elasticsearchVersion}.1-SNAPSHOT"

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.yaml-rest-test'
apply plugin: 'elasticsearch.testclusters'

// license of this project
licenseFile = rootProject.file('LICENSE.txt')
// copyright notices
noticeFile = rootProject.file('NOTICE.txt')

// disable uploadArchives task for now, no upload happening currently
uploadArchives.enabled = false

esplugin {
  // license of the plugin, may be different than the above license
  licenseFile rootProject.file('LICENSE.txt')
  // copyright notices, may be different than the above notice
  noticeFile rootProject.file('NOTICE.txt')
  name 'ingest-opennlp'
  description 'Ingest processor that uses OpenNLP for named entity extraction'
  classname 'de.spinscale.elasticsearch.ingest.opennlp.IngestOpenNlpPlugin'
}

githubRelease.doFirst {
  if (!System.getProperty('GITHUB_TOKEN', '')) {
    throw new Exception('Missing property GITHUB_TOKEN')
  }

  // check if zip file is there
  assert file("build/distributions/ingest-opennlp-${version}.zip").exists()

  // rename zip file
  def currentVersion = version.replace('-SNAPSHOT', '')
  def filename = "build/distributions/ingest-opennlp-${currentVersion}.zip"
  Files.copy(file("build/distributions/ingest-opennlp-${version}.zip").toPath(), file(filename).toPath())

  // configuration
  github {
    owner = 'spinscale'
    repo = 'elasticsearch-ingest-opennlp'
    token = System.getProperty('GITHUB_TOKEN')
    tagName = currentVersion
    assets = [ filename ]
  }
}

// In this section you declare the dependencies for your production and test code
// Elasticsearch dependency is included due to the build-tools, test-framework as well
dependencies {
  compile 'org.apache.opennlp:opennlp-tools:1.9.3'

  // the yaml tests require a log4j2 dependency, otherwise a dependency is thrown on startup
  yamlRestTestImplementation 'org.apache.logging.log4j:log4j-core:2.11.1'
}

testClusters.all {
  setting 'ingest.opennlp.model.file.names',     'en-ner-persons.bin'
  setting 'ingest.opennlp.model.file.locations', 'en-ner-locations.bin'
  setting 'ingest.opennlp.model.file.dates',     'en-ner-dates.bin'

  cliSetup 'ingest-opennlp/download-models'
}

// check style can be disabled, or you can configure a different checkstyle file
// checkstyleMain.enabled = false
// checkstyleTest.enabled = false

// dependency license check needs can be enabled
dependencyLicenses.enabled = false

// thirdparty audit needs can be enabled
thirdPartyAudit.enabled = false

// license header checks can be disabled
licenseHeaders.enabled = false

// no need to validate pom, as we do not upload to sonatype
validateNebulaPom.enabled = false

// added in 7.10 to allow for build
loggerUsageCheck.enabled = false

// exlude the models for forbidden API check
forbiddenPatterns {
  exclude '**/*.bin'
}

// ignore javadoc errors for now
tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

// download the models before the tests are run, also needed for packaging
project.afterEvaluate {
  processTestResources.dependsOn downloadModels
}

// download the models but dont overwrite existing ones
task downloadModels {
  doLast {
    downloadIfNotExists('http://opennlp.sourceforge.net/models-1.5/en-ner-person.bin',   'en-ner-persons.bin')
    downloadIfNotExists('http://opennlp.sourceforge.net/models-1.5/en-ner-location.bin', 'en-ner-locations.bin')
    downloadIfNotExists('http://opennlp.sourceforge.net/models-1.5/en-ner-date.bin',     'en-ner-dates.bin')
  }
}

def downloadIfNotExists(String url, String file) {
  String dir = rootDir.getAbsolutePath() + File.separator + 'src' + File.separator + 'test' + File.separator + 'resources' + File.separator + 'models'
  new File(dir).mkdirs()
  if (new File(dir + File.separator + file).exists() == false) {
    download {
      src url
      dest new File(dir, file)
    }
  }
}
